buildscript {
	repositories {
		mavenLocal()
		maven { url "https://repo.grails.org/grails/core" }
	}
	dependencies {
		classpath "org.grails:grails-gradle-plugin:$grailsVersion"
		classpath "org.grails.plugins:hibernate5:7.0.0"
	}
}


version "3.0.0-SNAPSHOT"
group "org.grails.plugins"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"org.grails.grails-plugin"
apply plugin:"org.grails.grails-plugin-publish"
apply plugin: 'maven-publish'

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
	groovydocconf.extendsFrom(runtime)
}
groovydoc {
	def title = "Grails Hibernate Search plugin ${version}"
	groovyClasspath = project.configurations.groovydocconf
}

jar {
	exclude 'hibernate.search/**'
}

repositories {
	mavenLocal()
	maven { url "https://repo.grails.org/grails/core" }
	maven {url "https://jenkins.cs.ox.ac.uk/artifactory/libs-release"}
}

dependencies {
	developmentOnly("org.springframework.boot:spring-boot-devtools")
	compileOnly "io.micronaut:micronaut-inject-groovy"
	console "org.grails:grails-console"
	compile "org.springframework.boot:spring-boot-starter-logging"
	compile "org.springframework.boot:spring-boot-starter-validation"
	compile "org.springframework.boot:spring-boot-autoconfigure"
	compile "org.grails:grails-core"
	profile "org.grails.profiles:plugin"
	testCompile "io.micronaut:micronaut-inject-groovy"
	testCompile "org.grails:grails-gorm-testing-support"
	testCompile "org.mockito:mockito-core"

	compile "org.grails:grails-logging"

	compile "org.grails.plugins:hibernate5"
	compile "org.hibernate:hibernate-core:5.4.4.Final"
	compile "org.hibernate:hibernate-ehcache:5.4.4.Final"

	compile "org.hibernate.search:hibernate-search-mapper-orm:6.0.8.OXBRC"
	//compile "org.hibernate.search:hibernate-search-v5migrationhelper-orm:6.0.8.OXBRC" // Migration helper

	compile "org.grails:grails-plugin-services"
	compile "org.grails:grails-plugin-domain-class"
	compile "org.grails:grails-plugin-datasource"

	groovydocconf 'org.fusesource.jansi:jansi:1.13'
	groovydocconf 'org.codehaus.groovy:groovy-all:3.0.8'

}

bootRun {
	ignoreExitValue true
	jvmArgs(
		'-Dspring.output.ansi.enabled=always',
		'-noverify',
		'-XX:TieredStopAtLevel=1',
		'-Xmx1024m')
	sourceResources sourceSets.main
	String springProfilesActive = 'spring.profiles.active'
	systemProperty springProfilesActive, System.getProperty(springProfilesActive)jvmArgs('-Dspring.output.ansi.enabled=always')
}

tasks.withType(GroovyCompile) {
	configure(groovyOptions) {
		forkOptions.jvmArgs = ['-Xmx1024m']
	}
}

tasks.withType(Test) {
	useJUnitPlatform()
}
// enable if you wish to package this plugin as a standalone application
bootJar.enabled = false

grailsPublish {
	// user/key: useless, see https://medium.com/@benorama/how-to-publish-your-grails-3-plugin-to-bintray-c341b24f567d#.elz43j5nq
	user = 'lgrignon'
	githubSlug = 'mathpere/grails-hibernate-search-plugin'
	license { name = 'Apache-2.0' }
	title = "Grails Hibernate Search Plugin"
	desc = "This plugin aims to integrate Hibernate Search features to Grails in very few steps."
	developers = [mathpere: "Mathieu Perez", lgrignon: "Louis Grignon"]
}

bintray {
	pkg {
		name = "hibernate-search"
	}
}

publishing {
	publications {
		mavenJar(MavenPublication) {
			groupId 'org.grails.plugins'
			artifactId 'hibernate-search'

			pom {
				name = 'hibernate-search'
				description = 'This plugin aims to integrate Hibernate Search features to Grails in very few steps.'
				url = 'https://github.com/mathpere/grails-hibernate-search-plugin'
				licenses {
					license {
						name = 'The Apache License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}
				developers {
					developer {
						id = 'mathpere'
						name = 'Mathieu Perez'
					}
					developer {
						id = 'lgrignon'
						name = 'Louis Grignon'
					}
					developer {
						id = 'olliefreeman'
						name = 'Oliver Freeman'
						email = 'ollie.freeman@gmail.com'
					}
				}
				scm {
					connection = 'git@github.com:mathpere/grails-hibernate-search-plugin.git'
					developerConnection = 'git@github.com:mathpere/grails-hibernate-search-plugin.git'
					url = 'https://github.com/mathpere/grails-hibernate-search-plugin'
				}
			}

			from components.java
			artifact sourcesJar
			artifact javadocJar
		}
	}
}
if (project.hasProperty('hibernateSearchMavenUser') && project.hasProperty('hibernateSearchMavenPassword') && project.hasProperty('hibernateSearchMavenRepoUrl')) {
	println "Configure publish tasks"
	publishing {
		repositories {
			maven {
				credentials {
					username "$hibernateSearchMavenUser"
					password "$hibernateSearchMavenPassword"
				}
				url project.version.endsWith('SNAPSHOT') ? "$hibernateSearchMavenRepoUrl/libs-snapshot-local" : "$hibernateSearchMavenRepoUrl/libs-release-local"
			}
		}

	}
}
